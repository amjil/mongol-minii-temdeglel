(ns mongol-drafts.desktop.widgets.drafts
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["package:flutter_context_menu/flutter_context_menu.dart" :as context-menu]
   [cljd.flutter :as f]
   [mongol-drafts.desktop.widgets.menu :as menu]
   [mongol-drafts.states.draft :as draft]
   [mongol-drafts.states.tag :as tag]
   [mongol-drafts.states.global :as gs]))

(defn list-item [i item]
  (f/widget
   :context ctx
   :get {navigator m/Navigator}
   (m/GestureDetector
    .onTap (fn []
             (.pushNamed navigator "/")
             (draft/set-current-draft-without-controller (get item "id"))
             (swap! gs/state assoc :menu-index "write")
             (tag/get-draft-tags gs/state (get item "id"))
             nil)) 
   (context-menu/ContextMenuRegion
    .contextMenu (menu/item-menu item)
    .onItemSelected (partial menu/item-selected-fn item i))
   (m/Container
    .width 40)
   (m/Column
    .children
    [(m/Icon m.Icons/article_outlined)
     (m/SizedBox .height 10)
     (m/Expanded
      .child
      (mgl/MongolText
       (if (empty? (get item "content"))
         "--blank--"
         (let [^String orig-content (get item "content")
               ^String content (.trim orig-content)]
           content))
       .maxLines 1
       .softWrap true
       .overflow m.TextOverflow/ellipsis
       .style (m/TextStyle
               .fontSize 14)))
     (if (= 1 (-> item (get "flag")))
       (m/Icon (.flag m/Icons)
               .size 12
               .color m.Colors/red)
       (m/Container))])))

(defn list-view []
  (f/widget
   :get {{{primary-color .-primary} .-colorScheme} m/Theme}
   :watch [{{contents :contents} :drafts} gs/state]
   (m.ListView/separated
    .scrollDirection m.Axis/horizontal
    .separatorBuilder (fn [context i]
                        (m/VerticalDivider
                         .color primary-color))
    .itemCount (count contents)
    .itemBuilder (fn [context i]
                   (list-item i (nth contents i)))
    .shrinkWrap true)))